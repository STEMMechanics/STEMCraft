plugins {
    id 'base'   // root is just a container, no jar
}

subprojects {
    group = 'dev.stemcraft'
    version = '1.0.0'

    repositories {
        mavenCentral()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
    }

    def targetJavaVersion = 17

    // Only runs in projects that apply 'java' or 'java-library'
    plugins.withId("java") {
        java {
            def javaVersion = JavaVersion.toVersion(targetJavaVersion)
            sourceCompatibility = javaVersion
            targetCompatibility = javaVersion
            if (JavaVersion.current() < javaVersion) {
                toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
            }
        }

        tasks.withType(JavaCompile).configureEach {
            options.encoding = 'UTF-8'
            if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
                options.release.set(targetJavaVersion)
            }
        }
    }
}